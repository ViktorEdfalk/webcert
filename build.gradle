plugins {
    id "se.inera.intyg.plugin.common" version "1.0.62" apply false
    id "org.akhikhl.gretty" version "1.4.2" apply false
    id "net.ltgt.apt" version "0.13" apply false
    id "com.moowork.node" version "1.1.1" apply false
}

ext {
    commonVersion = System.properties['commonVersion'] ?: '0-SNAPSHOT'
    infraVersion = System.properties['infraVersion'] ?: '0-SNAPSHOT'

    commonsIoVersion = "2.4"

    schemasContractVersion = "2.1.7"

    funktionstjansterGrpSchemasVersion = "1.0.3"
    intygClinicalprocessSchemasVersion = "1.0.12"
    ppSchemasVersion = "1.0.4"
    ppTermsSchemasVersion = "1.0.4"
    rivtaClinicalprocessSchemasVersion = "3.2.0-RC3"
    rivtaItintegrationMonitoringSchemasVersion = "1.0.0.3"

    activeMqVersion = "5.13.4"
    assertjVersion = "3.6.2"
    aspectJWeaverVersion = "1.7.1"
    autovalueVersion = "1.5"
    awaitilityVersion = "1.4.0"
    camelVersion = "2.16.0"
    dbUnitVersion = "2.4.9"
    geronimoJmsVersion = "1.1.1"
    hamcrestVersion = "1.3"
    httpComponentsVersion = "4.5"
    jadiraUserTypeVersion = "5.0.0.GA"
    javaxMailVersion = "1.4"
    jaxrsVersion = "1.5.4"
    jsonSchemaValidatorVersion = "2.8.0"
    liquibaseVersion = "3.3.5"
    // Note that Apache Ignite will pull in its own version of lucene, we're using exclude in /infra to prevent that.
    luceneAnalyzersCommonVersion = "5.5.2"
    openWsVersion = "1.4.2-1"
    opensamlVersion = "2.5.3"
    restassuredVersion = "2.8.0"
    servletApiVersion = "2.5"
    springDataVersion = "1.11.12.RELEASE"
    springSecurityVersion = "4.2.5.RELEASE"
    springVersion = "4.3.16.RELEASE"
    stAntlr4Version = "4.0.8"
    xmlToolingVersion = "1.3.2"
    xmlUnitVersion = "1.4"
    shedlockSpringVersion = "0.18.2"

    mockitoVersion = "2.16.0"
    springockitoVersion = "1.0.8"
    mysqlVersion = "5.1.40"
    h2Version = "1.3.176"
}

allprojects {
    apply plugin: 'se.inera.intyg.plugin.common'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'net.ltgt.apt'

    repositories {
        mavenLocal()
        maven { url "https://build-inera.nordicmedtest.se/nexus/repository/releases/" }
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    dependencies {
        testCompile 'junit:junit:4.+'
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
    }

    group = 'se.inera.intyg.webcert'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    uploadArchives.repositories.mavenDeployer {
        repository(url: "https://build-inera.nordicmedtest.se/nexus/repository/releases/") {
            authentication(userName: System.properties['nexusUsername'], password: System.properties['nexusPassword'])
        }
    }

    [install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
        pom.dependencies.findAll { dep -> dep.groupId == 'se.inera.intyg.common' }.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.common')
        }
        pom.dependencies.findAll { dep -> dep.groupId == 'se.inera.intyg.infra' }.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.infra')
        }
    }
}

task tagRelease(type: TagReleaseTask)
